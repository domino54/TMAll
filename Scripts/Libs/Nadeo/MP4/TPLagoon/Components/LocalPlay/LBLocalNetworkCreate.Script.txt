#Include "Libs/Nadeo/Common/FormDisplayer.Script.txt"	as FormDisplayer
#Include "TextLib"										as TextLib

#Const C_SettingsTypes		["Entry",			"Password",					"Password",							"Integer",			"Comment"]
#Const C_SettingsNames		[_("Game name :"),	_("Password (optional) :"),	_("Password spectator (opt.) :"),	_("Max players :"),	_("Comment :")]

Text Build() {
	//L16N |Infinitive| In a popup, a button to make a new server/new game room
	declare CreateText = _("Create");
	//L16N |Infinitive| Title of the form, to make a new game room for players to join
	FormDisplayer::Init(_("Create a server"), CreateText, "LocalNetwork_Create", C_SettingsTypes, C_SettingsNames, <-54.7,8.>, <216.7, 123.3>);
	FormDisplayer::SetDefault([0 => LocalUser.Login, 3 => "16"]);

	declare Text MLText = """
<manialink version="3" name="L_LocalNetwork_Create">
{{{FormDisplayer::ManialinkFrameModels()}}}

	<frame id="Frame_All">
{{{FormDisplayer::ManialinkMainBody()}}}
	</frame>

	<script><!--
{{{FormDisplayer::Includes()}}}
{{{FormDisplayer::Declares()}}}

	Void ShowPage()
	{
		EnableMenuNavigation(True,False,Null,1);
		FormDisplayer_Show();
		SendCustomEvent("LocalNetworkCreate_GetDefaultValues", [""]);
	}

	Void Init()
	{

	}

	main() {
		Init();
		FormDisplayer_Init();
		ShowPage();
		while(True) {

			yield;
			if(! PageIsVisible) continue;
			FormDisplayer_Loop();

			foreach(Event in PendingEvents) {
				switch(Event.Type) {
					case CMlEvent::Type::MenuNavigation: {
						switch(Event.MenuNavAction) {
							case CMlEvent::EMenuNavAction::Cancel : {
								SendCustomEvent("GotoPrevious", [""]);
							}
						}
					}
					case CMlEvent::Type::PluginCustomEvent: {
						switch(Event.CustomEventType) {
							case "Show" : {
								ShowPage();
								SendCustomEvent("UpdateBarreSection", ["LocalNetworkCreate"]);
							}
							case "Back" : {
								SendCustomEvent("GotoPrevious", Text[]); // TODO : Lié au hack constaté dans LocalPlayController
							}
							case "Hide" : {
								EnableMenuNavigation(True,False,Null,0);
								SendCustomEvent("DetachPage", ["LocalNetworkCreate"]);
							}
						}
					}
				}
			}
		}
	}
	--></script>
</manialink>	""";
	
	return MLText;
}