#Const C_MusicsBase	"file://Media/Images/TitlePackMaker/BrowseTracks/"
 
Text Build() {

	
	declare Text MLText = """
<manialink version="3" name="L_Music">
	<script><!--
		declare CAudioSource G_Music;
		
		Void StopMusic()
		{
			if (G_Music != Null)
			{
				G_Music.Stop();
				Audio.DestroySound(G_Music);
				G_Music = Null;
			}
			Audio.LimitMusicVolumedB = -100.;
		}

		Void PlayMusic(Text _MusicPath)
		{
			if (G_Music != Null)
			{
				G_Music.Stop();
				Audio.DestroySound(G_Music);
				G_Music = Null;
			}
			if (_MusicPath == "") {
				return;
			}
			Audio.LimitMusicVolumedB = 0.;
			G_Music = Audio.CreateSound(_MusicPath, 1., True, True, False);
			G_Music.Stop();
			G_Music.Play();
		}

		main() {
			G_Music = Null;
			Audio.LimitMusicVolumedB = -100.;
			
			while(True) {
				
				yield;
				if(! PageIsVisible) continue;
				
				foreach(Event in PendingEvents) {
					switch(Event.Type) {
						case CMlEvent::Type::PluginCustomEvent: {
							
							switch(Event.CustomEventType) {
								case "PlayMusic" : {
									PlayMusic(Event.CustomEventData[0]);
									
								}
								
								case "StopMusic" : {
									StopMusic();
								}
							}
						}
					}
				}
			}
		}
	--></script>
</manialink>""";
	
	return MLText;
}