#Include "Libs/Nadeo/MP4/Common/Components/AssetMaker/LBAssetMaker.Script.txt"	as AssetMaker
#Include "TextLib"											as TextLib

#Const C_DefaultAssetPositions	<-130., 29.>
#Const C_UGCAssetPositions		<-130., -22.>
//L16N Refers to game modes represented as scripts, to be played in the local play section.
#Const C_DefaultAssetTitle		_("|Max 20 characters|Default modes")
//L16N Refers to game modes represented as scripts, to be played in the local play section.
#Const C_UGCAssetTitle			_("|Max 20 characters|Custom modes")
#Const C_DefaultAssetText		_("These are the classic Trackmania mode scripts.")
//L16N Refers to scripts, editors, files.
#Const C_DefaultAssetEnding		_("You can keep or discard them from your title.")
#Const C_UGCAssetText			_("These are custom mode scripts.")
//L16N Refers to scripts, editors, files.
#Const C_UGCAssetEnding			_("You can add them to your title by clicking on the '+'.")

#Const C_Logo					"file://Media/MEDIABROWSER_HiddenResources/Common/Images/Menus/Buttons/Logo_LocalPlay.dds"
#Const C_AssetLogo				""

Text Build() {
	//L16N Title of a section. Refers to game modes represented as scripts, to be played in the local play section.
	declare Text[] AssetName = [_("Local modes"),
	//L16N |Verb, infinitive| refers to game modes represented as scripts, to be played in the local play section.
	_("Choose local mode")];
	AssetMaker::Init("LocalPlay", AssetName, C_DefaultAssetTitle,
			TextLib::Compose("%1 %2", C_DefaultAssetText, C_DefaultAssetEnding),
			C_UGCAssetTitle,
			TextLib::Compose("%1 %2", C_UGCAssetText, C_UGCAssetEnding),
		"", False, "", "", "", C_Logo, C_AssetLogo);
	declare Text MLText = """
<manialink version="3" name="L_LocalPlay">
{{{AssetMaker::ManialinkFrameModels()}}}

	<frame id="Frame_All">
{{{AssetMaker::ManialinkMainBody()}}}
	</frame>

	<script><!--
	#Include "TextLib" as LocalPlay_TextLib
	{{{AssetMaker::Includes()}}}
	{{{AssetMaker::Declares()}}}

	Void ShowPage()
	{
		EnableMenuNavigation(True,False,Null,1);
	}

	main() {
		Asset_Init();
		ShowPage();
		while(True) {

			yield;
			Asset_Loop();
			if(! PageIsVisible) continue;
			foreach(Event in PendingEvents) {
				switch(Event.Type) {
					case CMlEvent::Type::MenuNavigation: {
						switch(Event.MenuNavAction) {
							case CMlEvent::EMenuNavAction::Cancel : {
								SendCustomEvent("GotoPrevious", [""]);
							}
						}
					}
					case CMlEvent::Type::PluginCustomEvent: {
						switch(Event.CustomEventType) {
							case "Show" : {
								ShowPage();
								SendCustomEvent("UpdateBarreSection", ["LocalPlay"]);
								SendCustomEvent("TaskBar_UpdateTitleAndLogo", [LocalPlay_TextLib::ToText(False)]);
								Asset_Show();
							}
							case "Back" : {
								SendCustomEvent("GotoPrevious", Text[]);
							}
							case "Hide" : {
								EnableMenuNavigation(False,False,Null,0);
								SendCustomEvent("DetachPage", ["LocalPlay"]);
							}
						}
					}
				}
			}
		}
	}
	--></script>
</manialink>	""";
	
	return MLText;
}