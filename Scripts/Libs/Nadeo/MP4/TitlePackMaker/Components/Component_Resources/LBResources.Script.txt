#Include "Libs/Nadeo/MP4/Common/DesignManialinks.Script.txt"		as DesignManialinks
#Include "Libs/Nadeo/MP4/Common/ManialinkTools/FontManager.Script.txt" 			as Font

#Const C_TitleName				_("Title Name")
#Const C_Environment			_("|Game|Environment")
#Const C_TitleDescription		_("Title Description")
#Const C_FlashURL				_("Flash URL")
#Const C_ResourcesFolder		_("Resources Folder")
#Const C_TitleAdminPage			_("Title Manager Page")
#Const C_AdvancedConfiguration	_("Advanced configuration")
#Const C_DownloadURL			_("Download URL")
#Const C_Folders				_("Folders")
#Const C_FoldersExplanation		_("By default, all the files included in the Resources Folder will be added to your title pack.")
#Const C_TitleManager			_("Title Manager")
#Const C_TitleManagerHelp		_("By default, all the files included in the Resources Folder will be added to your title pack.")

#Const C_ImgBase				"file://Media/MEDIABROWSER_HiddenResources/"

#Const C_MargingY				10.

Text Build() {
	declare Text IMG_Arrow					= C_ImgBase ^ "Common/Images/Ingame/Arrow.dds";
	declare Text IMG_OpenInExplorer			= C_ImgBase ^ "Maker/Images/Resources/OpenInExplorer.dds";
	declare Text IMG_OpenInExplorer_Over	= C_ImgBase ^ "Maker/Images/Resources/OpenInExplorer_Over.dds";
	declare Text IMG_ConditionFlag			= C_ImgBase ^ "Common/Images/Menus/Buttons/Warning.dds";
	declare Text SubFont					= Font::GetFontName("SubTitle");

	declare Text MLText = """
<manialink version="3" name="L_ResourcesMenu">
<frame z-index="-6">
	<frame pos="-110 30">
		<frame pos="0 17.5">
			<label text="{{{C_TitleName}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>

			<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
			<quad z-index="0" size="207 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
			<frame pos="56 0" z-index="1">
				<entry id="ProjectName" pos="83 -2.2" size="140 7" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}" scriptevents="1" />
			</frame>
		</frame>
		<frame pos="0 5">
			<label text="{{{C_Environment}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>

			<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
			<quad z-index="0" size="207 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
			<frame pos="56 0" z-index="1">
				<label id="ProjectEnviro" pos="83 -2.5" size="140 7" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}"/>
			</frame>
		</frame>
		<frame pos="0 -7.5">
			<label text="{{{C_TitleDescription}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>

			<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
			<quad z-index="0" size="207 31" pos="5 2" opacity="0.1" bgcolor="000000"/>
			<frame pos="56 0" z-index="1">
				<textedit id="ProjectDescription" pos="83 -13.5" size="140 30" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}"/>
			</frame>
		</frame>
		<frame pos="0 -45">
			<frame pos="0 {{{C_MargingY*0}}}">
				<label text="{{{C_FlashURL}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>
				<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
				<quad z-index="0" size="167 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
				<frame pos="56 0" z-index="1">
					<entry id="FlashURL" pos="64 -2.2" size="101 7" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}" scriptevents="1" />
					{{{DesignManialinks::ManialinkExpandingButtonFrame(<136.7, -2.0>, <38., 8.5>, "", "", [], "FlashFolder")}}}
				</frame>
			</frame>
			<frame pos="0 {{{C_MargingY*-1}}}">
				<label text="{{{C_ResourcesFolder}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>

				<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
				<quad z-index="0" size="207 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
				<frame pos="56 0" z-index="1">
					<label pos="83 -2.5" size="140 5" id="TitleResourcesFolder" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}"/>
				</frame>
				<quad id="BrowseAllInExplorer" pos="218 2" z-index="0" size="8 8" bgcolor="FFFA" image="{{{IMG_OpenInExplorer}}}"
				scriptevents="1" imagefocus="{{{IMG_OpenInExplorer_Over}}}"/>
			</frame>
			<frame pos="0 {{{C_MargingY*-2}}}">
				<label text="{{{C_TitleAdminPage}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>
				<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
				<quad z-index="0" size="207 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
				<frame pos="56 0" z-index="1">
					<label id="TitleManagerPage" pos="83 -2.5" size="140 5" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}"/>
				</frame>
				<quad id="OpenLinkInInternet" pos="218 2" z-index="0" size="8 8" bgcolor="FFFA" style="Icons128x128_1" substyle="Credits" scriptevents="1"/>
			</frame>
			<frame pos="0 {{{C_MargingY*-3}}}">
				<label text="{{{C_AdvancedConfiguration}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>
				<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
				<quad z-index="0" size="207 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
				<frame pos="56 0" z-index="1">
					<label text="Resources.xml" pos="83 -2.5" size="140 5" id="TitleAdvancedConfig" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}"/>
				</frame>
				<quad id="BrowseAdvancedConfigInExplorer" pos="218 2" z-index="0" size="8 8" bgcolor="FFFA" image="{{{IMG_OpenInExplorer}}}"
				scriptevents="1" imagefocus="{{{IMG_OpenInExplorer_Over}}}"/>
			</frame>
			<frame pos="0 {{{C_MargingY*-4}}}">
				<label text="{{{C_DownloadURL}}}"		pos="10 0" textcolor="FFF" textfont="{{{SubFont}}}"/>
				<quad id="Quad_Arrow" halign="left"   valign="center" size="3.5 3.5" pos="64.8 -2" z-index="1" opacity="0.5" image="{{{IMG_Arrow}}}" keepratio="Fit"/>
				<quad z-index="0" size="207 8.5" pos="5 2" opacity="0.1" bgcolor="000000"/>
				<frame pos="56 0" z-index="1">
					<entry id="DownloadURL" pos="83 -2.2" size="140 7" textcolor="FFF" halign="center" valign="center" textfont="{{{SubFont}}}" scriptevents="1" />
				</frame>
			</frame>
		</frame>
		<frame id="BgQuad">
			<quad size="233 127.5" bgcolor="000" opacity="0.3" pos="0 22.5"/>
			<quad size="233 127.5" pos="0 22.5" opacity="0" style="Bgs1" substyle="BgDialogBlur" bluramount="0.1"/>
		</frame>
		<frame id="FrameConditions" pos="-41.7 -84">
			<quad id="ConditionFlag" halign="center" valign="center" size="5.5 5.5" pos ="5 -4" image="{{{IMG_ConditionFlag}}}" keepratio="Fit" opacity="0.7" colorize="aaa"/>
			<quad size="41 21" z-index="-1" bgcolor="FFF" opacity="0.4"/>
			<quad size="41 21" z-index="-1" opacity="0" style="Bgs1" substyle="BgDialogBlur" bluramount="0.1"/>
			<label id="ConditionsTextTitle" halign="left" valign="center" size="24 4" pos="10 -4" textprefix="$t" text="{{{C_Folders}}}" textsize="1.5" textfont="{{{SubFont}}}" textcolor="{{{DesignManialinks::GetColorCode_Overall()}}}"/>
			<label id="ConditionsTextLabel" halign="left" valign="top" size="27 10" pos="10 -8" text="{{{C_FoldersExplanation}}}" textsize="0.5" textfont="{{{SubFont}}}" textcolor="{{{DesignManialinks::GetColorCode_Overall()}}}" autonewline="1"/>
		</frame>
	</frame>
</frame>
	<script><!--
	{{{DesignManialinks::Includes()}}}
	{{{DesignManialinks::Declares()}}}
		declare Text G_ProjectName;
		declare Text G_ProjectEnviro;
		declare Text G_ProjectId;
		declare Text G_TitleManagerPage;
		declare Text G_ProjectDesc;
		declare Text G_DownloadURL;
		declare Text G_FlashURL;

		Void UpdateButtonsDisplay()
		{
			declare ProjectName			<=> (Page.GetFirstChild("ProjectName") as CMlEntry);
			declare ProjectEnviro		<=> (Page.GetFirstChild("ProjectEnviro") as CMlLabel);
			declare ProjectDescription	<=> (Page.GetFirstChild("ProjectDescription") as CMlTextEdit);
			declare TitleManagerPage	<=> (Page.GetFirstChild("TitleManagerPage") as CMlLabel);
			declare TitleResourcesFolder <=> (Page.GetFirstChild("TitleResourcesFolder") as CMlLabel);
			declare DownloadURL			<=> (Page.GetFirstChild("DownloadURL") as CMlEntry);
			declare FlashURL			<=> (Page.GetFirstChild("FlashURL") as CMlEntry);
			ProjectName.Value          = G_ProjectName;
			ProjectEnviro.Value        = G_ProjectEnviro;
			ProjectDescription.Value   = G_ProjectDesc;
			TitleManagerPage.Value     = G_TitleManagerPage;
			TitleResourcesFolder.Value = "WorkTitles/" ^ G_ProjectId ^ "/";
			DownloadURL.Value          = G_DownloadURL;
			FlashURL.Value             = G_FlashURL;

			declare Integer Page_FlashURLFocus		for Page;
			DesignManialinks_ExpandingButton_SelectAnswer("FlashFolder", Page_FlashURLFocus);
		}

		Void Update()
		{
			declare Text Page_ProjectDisplayName	for Page;
			declare Text Page_ProjectTitleId		for Page;
			declare Text Page_TitleManagerPage		for Page;
			declare Text Page_ProjectDescription	for Page;
			declare Text Page_DownloadURL			for Page;
			declare Integer Page_FlashURLFocus		for Page;
			declare Text[] Page_FlashURLList		for Page;
			G_ProjectName		= Page_ProjectDisplayName;
			G_ProjectId			= Page_ProjectTitleId;
			G_TitleManagerPage	= Page_TitleManagerPage;
			G_ProjectDesc		= Page_ProjectDescription;
			G_DownloadURL		= Page_DownloadURL;
			if(Page_FlashURLList.existskey(Page_FlashURLFocus))
			{
				//log("Page_FlashURLList["^Page_FlashURLFocus^"]: "^ Page_FlashURLList[Page_FlashURLFocus]);
				G_FlashURL = Page_FlashURLList[Page_FlashURLFocus];
			}
			UpdateButtonsDisplay();
		}

		Void Init() {
			Update();

			declare AttributeControl <=> Page.GetFirstChild("FlashFolder");
			DesignManialinks_ExpandingButton_Update(AttributeControl, ["Option0", "Option1", "Option2", "Option3"], [_("Title"), _("Author"), _("Environment"), _("Custom")]);
			DesignManialinks_Init();
		}

		Void SetFlashUrl(Integer _Index) {
			declare Text[] Page_FlashURLList for Page;
			declare Text NextFlashUrl;
			if(Page_FlashURLList.existskey(_Index)) NextFlashUrl = Page_FlashURLList[_Index];
			
			declare CMlEntry  FlashURL	<=> (Page.MainFrame.GetFirstChild("FlashURL") as CMlEntry);
			FlashURL.Value = NextFlashUrl;
			SendCustomEvent("Resources_SetFlashUrlCustom", [FlashURL.Value]);
			SendCustomEvent("Resources_SetFlashUrlFocus", [_Index ^ ""]);
		}

		main() {
			Init();
			declare Text ProjectEnviro for LocalUser;
			while(True) {
				yield;
				if (!PageIsVisible && PendingEvents.count == 0) continue;
				if (G_ProjectEnviro != ProjectEnviro) {
					G_ProjectEnviro = ProjectEnviro;
					UpdateButtonsDisplay();
				}
				DesignManialinks_Loop();

				foreach(Event in PendingEvents) {
					switch(Event.Type) {
						case CMlEvent::Type::EntrySubmit: {
							if (Event.ControlId == "FlashFolder_ExpandingButtonQuadEntry") {
								declare Text Value = (Event.Control as CMlEntry).Value; // Ca peut valoir "Option1", "Option2" ou "Option3"
								switch(Value)
								{
									case "Option0": SetFlashUrl(0);
									case "Option1": SetFlashUrl(1);
									case "Option2": SetFlashUrl(2);
									//case "Option3": SetFlashUrl(3);
								}
							}
							else if (Event.ControlId == "FlashURL") {
								declare CMlEntry URL <=> (Event.Control as CMlEntry);
								DesignManialinks_ExpandingButton_SelectAnswer("FlashFolder", 3);
								SendCustomEvent("Resources_SetFlashUrlCustom", [URL.Value]);
								SendCustomEvent("Resources_SetFlashUrlFocus", ["3"]);
							}
							else
							{
								SendCustomEvent("Maker_SaveDesc", Text[]); //aaaa
							}
						}
						case CMlEvent::Type::MouseClick: {
							if (Event.ControlId == "BrowseAllInExplorer") {
								SendCustomEvent("BrowseInExplorer", [""]);
							}
							if (Event.ControlId == "BrowseAdvancedConfigInExplorer") {
								SendCustomEvent("BrowseInExplorer", ["Media\\XML\\User\\"]);
							}
							if (Event.ControlId == "OpenLinkInInternet") {
								SendCustomEvent("OpenLink", [G_TitleManagerPage]);
							}
						}
						case CMlEvent::Type::MenuNavigation: {
							switch(Event.MenuNavAction) {
								case CMlEvent::EMenuNavAction::Cancel : {
									SendCustomEvent("GotoPrevious", [""]);
								}
							}
						}
						case CMlEvent::Type::PluginCustomEvent: {
							switch(Event.CustomEventType) {
								case "Show" : {
									SendCustomEvent("TaskBar_Show", Text[]);
									SendCustomEvent("Resources_Show", Text[]);
									EnableMenuNavigation(True,False,Null,1);
								}
								case "Back" : {
									SendCustomEvent("GotoPrevious", [""]);
								}
								case "Hide" : {
									EnableMenuNavigation(True,False,Null,0);
									SendCustomEvent("DetachPage", ["Resources"]);
								}
								case "Update" : {
									Update();
								}
							}
						}
					}
				}
			}
		}
	--></script>
</manialink>
	""";
	
	return MLText;
}