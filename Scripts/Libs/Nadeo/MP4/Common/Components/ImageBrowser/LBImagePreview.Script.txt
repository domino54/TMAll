Text Build() {
	declare Text MLText = """<manialink version="3" name="L_ImagePreview">

<frame id="Frame_All" z-index="80" hidden="1">
	<frame scale="0.8 0.8">
		<quad id="BgQuad" pos="-30 30" size="100 100" keepratio="fit"/>
	</frame>
	<quad id="Background" bgcolor="000" opacity="0.8" z-index="-1" pos="-160 90" size="320 180" scriptevents="1"/>
</frame>
	
	<script><!--
		declare Text G_ImageUrl;

		Void ShowPage()
		{
			Page.GetFirstChild("Frame_All").Visible = True;

			declare BgQuad <=> (Page.GetFirstChild("BgQuad") as CMlQuad);
			BgQuad.ChangeImageUrl(G_ImageUrl);

			EnableMenuNavigation(True,False,Null,1);
		}

		Void HidePage()
		{
			Page.GetFirstChild("Frame_All").Visible = False;
		}
		
		Void Init()
		{
			G_ImageUrl = "";
		}

		main() {
			Init();

			while(True) {
				
				yield;
				if(! PageIsVisible) continue;

				foreach(Event in PendingEvents) {
					switch(Event.Type) {

						case CMlEvent::Type::MenuNavigation: {
							switch(Event.MenuNavAction) {
								case CMlEvent::EMenuNavAction::Cancel : {
									HidePage();
								}
							}
						}
						case CMlEvent::Type::MouseClick: {
							if (Event.ControlId == "Background")
							{
								HidePage();
							}
						}
						case CMlEvent::Type::PluginCustomEvent: {
							switch(Event.CustomEventType) {
								case "Show" : {
									if (Event.CustomEventData.count != 1)
										continue;
									G_ImageUrl = Event.CustomEventData[0];
									ShowPage();
								}

								case "Hide" : {
									HidePage();
								}
							}
						}
					}
				}
			}
		}
	--></script>
</manialink>""";
	
	return MLText;
}