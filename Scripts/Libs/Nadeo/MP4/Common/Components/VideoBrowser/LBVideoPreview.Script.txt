Text Build() {
	declare Text MLText = """<manialink version="3" name="L_ImagePreview">

<frame id="Frame_All" z-index="80" hidden="1">
	<frame scale="0.5 0.5">
		<quad id="BgQuad" pos="-160 90" size="320 180"/>
	</frame>
	<quad id="Background" bgcolor="000" opacity="0.8" z-index="-1" pos="-160 90" size="320 180" scriptevents="1"/>
</frame>
	
	<script><!--
		declare Text G_VideoUrl;
		declare CVideo G_Video;

		Void ShowPage(Boolean _RefreshVideo)
		{
			declare BgQuad <=> (Page.GetFirstChild("BgQuad") as CMlQuad);
			if (_RefreshVideo) {
				if (G_Video != Null) {
					Video.DestroyVideo(G_Video);
					G_Video = Null;
				}
				if (G_VideoUrl != "") {
					G_Video = Video.CreateVideo(G_VideoUrl, True, True);
					BgQuad.Image = G_Video.Image;
				}
			} else if (G_Video != Null) {
				G_Video.Stop();
			}
			if (G_Video != Null) {
				G_Video.Play();
			}

			Page.GetFirstChild("Frame_All").Visible = True;
			EnableMenuNavigation(True,False,Null,1);
		}

		Void HidePage()
		{
			Page.GetFirstChild("Frame_All").Visible = False;
		}
		
		Void Init()
		{
			G_VideoUrl = "";
			G_Video = Null;
		}

		main() {
			Init();

			while(True) {
				
				yield;
				if(! PageIsVisible) continue;

				foreach(Event in PendingEvents) {
					switch(Event.Type) {

						case CMlEvent::Type::MenuNavigation: {
							switch(Event.MenuNavAction) {
								case CMlEvent::EMenuNavAction::Cancel : {
									HidePage();
								}
							}
						}
						case CMlEvent::Type::MouseClick: {
							if (Event.ControlId == "Background")
							{
								HidePage();
							}
						}
						case CMlEvent::Type::PluginCustomEvent: {
							switch(Event.CustomEventType) {
								case "Show" : {
									if (Event.CustomEventData.count != 1)
										continue;
									if (G_VideoUrl != Event.CustomEventData[0]) {
										G_VideoUrl = Event.CustomEventData[0];
										ShowPage(True);
									} else {
										G_VideoUrl = Event.CustomEventData[0];
										ShowPage(False);
									}
								}

								case "Hide" : {
									HidePage();
								}
							}
						}
					}
				}
			}
		}
	--></script>
</manialink>""";
	
	return MLText;
}