#Include "Libs/Nadeo/Common/Core/MenuLayers.Script.txt"											as Layers
#Include "Libs/Nadeo/Common/Core/Logger.Script.txt"												as Logger

Void ChangeData(Text _LayerNameToChangeData, Text[] _EventNames, Text[Text][] _ButtonMetadata, Boolean[] _ButtonIsActive,
	Vec2[] _Positions, Vec2[] _Sizes, Boolean _IsNavigationVertical) {
	declare CUILayer LayerToChangeData = Layers::Get(_LayerNameToChangeData);
	if (LayerToChangeData == Null) {
		Logger::Warn("ChangeData de MenuButtons : Layer non trouvée");
		return;
	}
	if ((_ButtonMetadata.count != _Positions.count) || (_Positions.count != _EventNames.count)) {
		Logger::Warn("ChangeData de MenuButtons : Les arrays n'ont pas le même nombre d'éléments");
		return;
	}
	declare Text[]			Page_MenuButtons_EventNames			for LayerToChangeData.LocalPage;
	declare Text[Text][]	Page_MenuButtons_Metadata			for LayerToChangeData.LocalPage;
	declare Boolean[]		Page_MenuButtons_ButtonIsActive		for LayerToChangeData.LocalPage;
	declare Vec2[]			Page_MenuButtons_ButtonPositions	for LayerToChangeData.LocalPage;
	declare Vec2[]			Page_MenuButtons_ButtonSizes		for LayerToChangeData.LocalPage;
	declare Boolean			Page_MenuButtons_IsNavVertical		for LayerToChangeData.LocalPage;
	Page_MenuButtons_EventNames			= _EventNames;
	Page_MenuButtons_Metadata			= _ButtonMetadata;
	Page_MenuButtons_ButtonIsActive		= _ButtonIsActive;
	Page_MenuButtons_ButtonPositions	= _Positions;
	Page_MenuButtons_ButtonSizes		= _Sizes;
	Page_MenuButtons_IsNavVertical		= _IsNavigationVertical;
	LayerCustomEvent(LayerToChangeData, "MenuButtons_ChangeData", Text[]);
}

Void SetButtonActive(Text _LayerNameToChangeData, Boolean[] _ButtonIsActive)
{
	declare CUILayer LayerToChangeData = Layers::Get(_LayerNameToChangeData);
	if (LayerToChangeData == Null) return;

	declare Boolean[]		Page_MenuButtons_ButtonIsActive		for LayerToChangeData.LocalPage;
	Page_MenuButtons_ButtonIsActive		= _ButtonIsActive;
	LayerCustomEvent(LayerToChangeData, "MenuButtons_ChangeData", Text[]);
}