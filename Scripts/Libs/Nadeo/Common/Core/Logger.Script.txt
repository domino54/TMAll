#Const Version 		"2014-07-28"
#Const ScriptName	"Logger.Script.txt"

declare Integer[Text] G_TimersStartNow;

Text GetScriptVersion() {return Version;}
Text GetScriptName() {return ScriptName;}

Void Private_Log(Text _Message) {
	log(Now^"> "^_Message);
}

Void Log(Text _Message) {
	Private_Log(_Message);
}
Void Log(Integer _Value) {
	Private_Log(""^_Value);
}
Void Log(Real _Value) {
	Private_Log(""^_Value);
}
Void Log(Boolean _Value) {
	Private_Log(""^_Value);
}
Void Log(CNod _Value) {
	Private_Log(""^_Value);
}

Void Warn(Text _Message) {
	Log("[WARNING] "^_Message);
}

Void StartCounter(Text _CounterLabel) {
	G_TimersStartNow[_CounterLabel] = Now;
	Log("START "^_CounterLabel);
}

Void TickCounter(Text _CounterLabel, Text _Comment) {
	if(! G_TimersStartNow.existskey(_CounterLabel)) {
		Warn(_CounterLabel^" not started" );
		return;
	}
	Log(" TICK "^_CounterLabel^" : "^((Now - G_TimersStartNow[_CounterLabel]) / 1000.)^"s "^_Comment);
}

Void TickCounter(Text _CounterLabel) {
	TickCounter(_CounterLabel, "");
}