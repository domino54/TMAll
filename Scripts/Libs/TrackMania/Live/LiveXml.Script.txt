#Include "Libs/Trackmania/Live/LiveLogger.Script.txt" as LiveLogger

Text Manialib_Xml_NodeToText(CXmlNode _Node)
{
	if(_Node == Null)
	{
		LiveLogger::Manialib_Logger_Debug("[Xml] Undefined node");
		return "";
	}
	return _Node.TextContents;
}
Text[Text] Manialib_Xml_NodeToArray(CXmlNode _Node)
{
	declare Text[Text] result;
	if(_Node == Null)
	{
		LiveLogger::Manialib_Logger_Debug("[Xml] Undefined node");
		return result;
	}
	foreach(child in _Node.Children)
	{
		result[child.Name] = Manialib_Xml_NodeToText(child);
	}
	return result;
}
Text[Text][] Manialib_Xml_NodeToArray2(CXmlNode _Node)
{
	declare Text[Text][] result;
	if(_Node == Null)
	{
		LiveLogger::Manialib_Logger_Debug("[Xml] Undefined node");
		return result;
	}
	foreach(child in _Node.Children)
	{
		result.add(Manialib_Xml_NodeToArray(child));
	}
	return result;
}
Text[Text][][] Manialib_Xml_NodeToArray3(CXmlNode _Node)
{
	declare Text[Text][][] result;
	if(_Node == Null)
	{
		LiveLogger::Manialib_Logger_Debug("[Xml] Undefined node");
		return result;
	}
	foreach(child in _Node.Children)
	{
		result.add(Manialib_Xml_NodeToArray2(child));
	}
	return result;
}
CXmlDocument Manialib_Xml_CreateDocument(Text _XmlText) {
	declare CXmlDocument Doc <=> Xml.Create(_XmlText);
	if(Doc == Null)
	{
		LiveLogger::Manialib_Logger_Debug("[Xml] Couldn't create CXmlDocument");
	}
	return Doc;
}

Void Manialib_Xml_DestroyDocument(CXmlDocument _Doc)
{
	if (_Doc != Null)
		Xml.Destroy(_Doc);
}