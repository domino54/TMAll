/**
 * @name PlayerModels_Server.Script.txt
 * @author domino54
 * @version 2024-07-19
 */

#Const Version      "2024-07-19"
#Const ScriptName   "Libs/domino54/PlayerModels_Server.Script.txt"

#Include "TextLib" as TL
#Include "Libs/Nadeo/Log.Script.txt" as Log
#Include "Libs/domino54/CustomSkins_Players.Script.txt" as CSPlayers
#Include "Libs/domino54/Openplanet_Players.Script.txt" as OPPlayers
#Include "Libs/domino54/PlayerModels_Data.Script.txt" as PMData

// ---------------------------------- //
// Constants
// ---------------------------------- //

#Const C_LibPlayerModels_CompatibilityMode_Disabled         0   ///< Loads items and archetypes normally.
#Const C_LibPlayerModels_CompatibilityMode_Strict           1   ///< Never loads items or archetypes, only loads skins onto the player map model.
#Const C_LibPlayerModels_CompatibilityMode_AllowArchetypes  2   ///< Same as Strict, but allows archetypes to load.

/// These environments restrict the assignable archetypes and require unlock via items.
#Const C_LibPlayerModels_EnvironmentRequiresUnlock ["Stadium"]

/// These archetypes are available without the need of an unlock.
#Const C_LibPlayerModels_VehicleIgnoresUnlock ["StadiumCar"]

/// Model name, which references whatever model is used on the loaded map.
#Const C_LibPlayerModels_DefaultPlayerModel "DefaultPlayerModel"

// ---------------------------------- //
// Global variables
// ---------------------------------- //

declare Text G_LibPlayerModels_CurrentMapEnvironmentName;
declare Text G_LibPlayerModels_CurrentMapPlayerModelName;
declare Text G_LibPlayerModels_CurrentMapPlayerModelId;

/**
 * As soon as the game mode loads any items, they will be referenced in the saved replays.
 * This can cause a TON of rererences and make the replays unplayable outside of the title pack.
 * Custom items should load only in game modes, where players can spawn as different mdoels on the same map.
 *
 * Therefore, the compatibility mode will not load any items or archetypes from the actual paths of the source files.
 *
 * Instead, all item paths are replaced with "DefaultPlayerModel", which references the current map model.
 * This model dynamically changes with the current map, but also locks the mode into a single model only.
 */
declare Integer G_LibPlayerModels_CompatibilityMode;

/// Lookup table for text-based model ID to loaded item Ident.
declare Ident[Text] G_LibPlayerModels_LoadedItemIdByLookupId;

/// Player model ID allowlist for specific models to be loaded exclusively.
declare Text[] G_LibPlayerModels_PlayerModelIdAllowlist;

/// Model skin ID allowlist for specific skins to be loaded exclusively.
declare Text[] G_LibPlayerModels_ModelSkinIdAllowlist;

// ---------------------------------- //
// Private functions
// ---------------------------------- //

Text Private_GetLookupId(Text _ModelId, Text _SkinId) {
  return """{{{_ModelId}}}:{{{_SkinId}}}""";
}

Text Private_GetLookupId(Text _ModelId, Boolean _IsArchetype) {
  if (_IsArchetype) {
    return """{{{_ModelId}}}:archetype""";
  }

  return _ModelId;
}

Text Private_GetLookupId(Text _ModelId) {
  return Private_GetLookupId(_ModelId, False);
}

Text Private_GetArchetypePath(Text _ItemPath) {
  if (G_LibPlayerModels_CompatibilityMode == C_LibPlayerModels_CompatibilityMode_Strict) {
    return C_LibPlayerModels_DefaultPlayerModel;
  }

  return _ItemPath;
}

Text Private_GetItemPath(Text _ItemPath) {
  if (G_LibPlayerModels_CompatibilityMode != C_LibPlayerModels_CompatibilityMode_Disabled) {
    return C_LibPlayerModels_DefaultPlayerModel;
  }

  return _ItemPath;
}

Boolean Private_IsModelAllowlisted(Text _ModelId) {
  return G_LibPlayerModels_PlayerModelIdAllowlist.count <= 0 || G_LibPlayerModels_PlayerModelIdAllowlist.exists(_ModelId);
}

Boolean Private_IsSkinAllowlisted(Text _SkinId) {
  return G_LibPlayerModels_ModelSkinIdAllowlist.count <= 0 || G_LibPlayerModels_ModelSkinIdAllowlist.exists(_SkinId);
}

Ident Private_ItemListAdd(CMode _Context, Text[] _ItemProps) {
  declare ResultId = NullId;

  switch (_ItemProps.count) {
    case 1: ResultId = _Context.ItemList_Add(_ItemProps[0]);
    case 2: ResultId = _Context.ItemList_AddWithSkin(_ItemProps[0], _ItemProps[1]);
  }

  assert(ResultId != NullId, "Player model could not be loaded.");

  return ResultId;
}

Void Private_LoadPlayerModelItems(CMode _Context, Text[] _ModelAllowlist, Text[] _SkinAllowlist) {
  G_LibPlayerModels_PlayerModelIdAllowlist = _ModelAllowlist;
  G_LibPlayerModels_ModelSkinIdAllowlist = _SkinAllowlist;

  // Dictionary of tuples. This is used later to call ItemList_Add.
  // [UniqueId => [ItemPath, SkinPath]]
  declare Text[][Text] ItemsToLoad = [];

  // Holds the item path for loading skins.
  declare Text[Text] BaseModelItemPath = [];

  // Prepare base player models.
  foreach (PlayerModelId => PlayerModel in PMData::GetAllPlayerModels()) {
    if (!Private_IsModelAllowlisted(PlayerModelId)) continue;

    declare AsItemId = Private_GetLookupId(PlayerModelId);

    if (PlayerModel.LoadAs == PMData::C_LibPlayerModels_LoadAs_Archetype) {
      BaseModelItemPath[AsItemId] = PlayerModel.Archetype;
    } else {
      BaseModelItemPath[AsItemId] = PlayerModel.ItemPath;
    }

    if (PlayerModel.LoadAs == PMData::C_LibPlayerModels_LoadAs_ItemAndArchetype || PlayerModel.LoadAs == PMData::C_LibPlayerModels_LoadAs_Item) {
      ItemsToLoad[AsItemId] = [Private_GetItemPath(PlayerModel.ItemPath)];
    }

    if (PlayerModel.LoadAs == PMData::C_LibPlayerModels_LoadAs_ItemAndArchetype || PlayerModel.LoadAs == PMData::C_LibPlayerModels_LoadAs_Archetype) {
      declare AsArchetypeId = Private_GetLookupId(PlayerModelId, True);

      ItemsToLoad[AsArchetypeId] = [Private_GetArchetypePath(PlayerModel.Archetype)];
    }
  }

  // Holds skin paths for loading overlays.
  declare Text[Text][Text] SkinsByBaseModelId = [];

  // Prepare skins.
  foreach (ModelSkinId => ModelSkin in PMData::GetAllModelSkins()) {
    if (!Private_IsSkinAllowlisted(ModelSkinId)) continue;

    // Skins flagged by IsDefault are fake and used only as a display of the base player model appearance.
    if (ModelSkin.IsDefault) {
      continue;
    }

    if (SkinsByBaseModelId.existskey(ModelSkin.AppliesTo)) {
      SkinsByBaseModelId[ModelSkin.AppliesTo][ModelSkinId] = ModelSkin.SkinPath;
    } else {
      SkinsByBaseModelId[ModelSkin.AppliesTo] = [ModelSkinId => ModelSkin.SkinPath];
    }
  }

  // Load skins for base models.
  foreach (BaseModelId => Skins in SkinsByBaseModelId) {
    // Can't skip the skins in the loop above, because overlays may be allowed.
    if (!Private_IsModelAllowlisted(BaseModelId)) continue;

    foreach (ModelSkinId => SkinPath in Skins) {
      // CarCommon wildcard: Load the skin for every base player model available.
      if (BaseModelId == PMData::C_LibPlayerModels_CarCommonWildcard) {
        foreach (ModelId => __ in BaseModelItemPath) {
          declare LookupId = Private_GetLookupId(ModelId, ModelSkinId);

          ItemsToLoad[LookupId] = [Private_GetItemPath(BaseModelItemPath[ModelId]), SkinPath];
        }
      }

      // Regular skin: Load for a single base player model.
      else {
        declare LookupId = Private_GetLookupId(BaseModelId, ModelSkinId);

        ItemsToLoad[LookupId] = [Private_GetItemPath(BaseModelItemPath[BaseModelId]), SkinPath];
      }
    }
  }

  // Prepare overlays.
  foreach (ModelOverlayId => ModelOverlay in PMData::GetAllModelOverlays()) {
    if (!Private_IsModelAllowlisted(ModelOverlayId)) continue;

    declare BaseModelLookupId = Private_GetLookupId(ModelOverlayId);

    ItemsToLoad[BaseModelLookupId] = [Private_GetItemPath(ModelOverlay.ItemPath)];

    // Overlay is using the same skins as the overlay's base player model.
    if (SkinsByBaseModelId.existskey(ModelOverlay.Extends)) {
      foreach (ModelSkinId => SkinPath in SkinsByBaseModelId[ModelOverlay.Extends]) {
        declare LookupId = Private_GetLookupId(ModelOverlayId, ModelSkinId);

        ItemsToLoad[LookupId] = [Private_GetItemPath(ModelOverlay.ItemPath), SkinPath];
      }
    }

    // If there are any CarCommon skins, apply them to the overlay.
    if (SkinsByBaseModelId.existskey(PMData::C_LibPlayerModels_CarCommonWildcard)) {
      foreach (ModelSkinId => SkinPath in SkinsByBaseModelId[PMData::C_LibPlayerModels_CarCommonWildcard]) {
        declare LookupId = Private_GetLookupId(ModelOverlayId, ModelSkinId);

        ItemsToLoad[LookupId] = [Private_GetItemPath(ModelOverlay.ItemPath), SkinPath];
      }
    }
  }

  // Load items into the game mode.
  declare Ident[Text] CachedItemIds;

  foreach (PlayerModelId => ItemProps in ItemsToLoad) {
    declare ReusableIndex = TL::Join("*", ItemProps);

    // If we get the same [Model+Skin], don't load the same item twice, reuse an existing Ident.
    if (CachedItemIds.existskey(ReusableIndex)) {
      log(PlayerModelId^" -> "^ItemProps^" (SKIPPED)");
      G_LibPlayerModels_LoadedItemIdByLookupId[PlayerModelId] = CachedItemIds[ReusableIndex];
      continue;
    }

    declare ResultId = Private_ItemListAdd(_Context, ItemProps);
    log(PlayerModelId^" -> "^ItemProps);
    CachedItemIds[ReusableIndex] = ResultId;
    G_LibPlayerModels_LoadedItemIdByLookupId[PlayerModelId] = ResultId;
  }

  declare NbItems = CachedItemIds.count;

  Log::Log("""[PlayerModels_Server] Loaded {{{NbItems}}} items""");
}

Boolean Private_CurrentMapRequiresUnlock() {
  return C_LibPlayerModels_EnvironmentRequiresUnlock.exists(G_LibPlayerModels_CurrentMapEnvironmentName);
}

Ident Private_GetPlayerModelLookupId(Text _ModelId, Text _SkinId, Boolean _PreferCustom) {
  // If not specified to use custom items, try to match an archetype to preserve the regular skins.
  if (!_PreferCustom) {
    declare Archetype = Private_GetLookupId(_ModelId, True);

    if (G_LibPlayerModels_LoadedItemIdByLookupId.existskey(Archetype)) {
      return G_LibPlayerModels_LoadedItemIdByLookupId[Archetype];
    }
  }

  // If a skin ID is specified, try to load the item with it.
  if (_SkinId != "") {
    declare WithSkin = Private_GetLookupId(_ModelId, _SkinId);

    if (G_LibPlayerModels_LoadedItemIdByLookupId.existskey(WithSkin)) {
      return G_LibPlayerModels_LoadedItemIdByLookupId[WithSkin];
    }
  }

  // Load the item with a basic skin.
  declare Item = Private_GetLookupId(_ModelId);

  if (G_LibPlayerModels_LoadedItemIdByLookupId.existskey(Item)) {
    return G_LibPlayerModels_LoadedItemIdByLookupId[Item];
  }

  // Nothing was found, fallback to no custom model.
  return NullId;
}

Void Private_AssignPlayerModel(CPlayer _Player, Ident _ModelId) {
  switchtype (_Player) {
    case CSmPlayer: {
      (_Player as CSmPlayer).ForceModelId = _ModelId;
    }
    case CTmPlayer: {
      (_Player as CTmPlayer).ForceModelId = _ModelId;
    }
    default: {
      assert(False, "Attempted to assign player model to unsupported player class instance.");
    }
  }
}

Void Private_AssignPlayerModel(CPlayer _Player, Text _ModelId, Text _SkinId, Boolean _PreferCustom) {
  declare ModelId = Private_GetPlayerModelLookupId(_ModelId, _SkinId, _PreferCustom);

  Private_AssignPlayerModel(_Player, ModelId);
}

// ---------------------------------- //
// Public functions
// ---------------------------------- //

Text GetScriptVersion() {
  return Version;
}

Text GetScriptName() {
  return ScriptName;
}

Void Unload(CMode _Context) {
  G_LibPlayerModels_LoadedItemIdByLookupId.clear();
  G_LibPlayerModels_PlayerModelIdAllowlist.clear();
  G_LibPlayerModels_ModelSkinIdAllowlist.clear();
}

Void DisableCompatibilityMode() {
  G_LibPlayerModels_CompatibilityMode = C_LibPlayerModels_CompatibilityMode_Disabled;
}

Void StrictCompatibilityMode() {
  G_LibPlayerModels_CompatibilityMode = C_LibPlayerModels_CompatibilityMode_Strict;
}

Void AllowArchetypesCompatibilityMode() {
  G_LibPlayerModels_CompatibilityMode = C_LibPlayerModels_CompatibilityMode_AllowArchetypes;
}

Void Load(CMode _Context, Text[] _ModelAllowlist, Text[] _SkinAllowlist) {
  Private_LoadPlayerModelItems(_Context, _ModelAllowlist, _SkinAllowlist);
}

Void Load(CMode _Context, Text[] _ModelAllowlist) {
  Load(_Context, _ModelAllowlist, []);
}

Void Load(CMode _Context) {
  Load(_Context, []);
}

Void SetMapProperties(Text _Environment, Text _MapPlayerModelName) {
  G_LibPlayerModels_CurrentMapEnvironmentName = _Environment;
  G_LibPlayerModels_CurrentMapPlayerModelName = _MapPlayerModelName;
  G_LibPlayerModels_CurrentMapPlayerModelId = PMData::GetPlayerModelIdByItemPath(_MapPlayerModelName);
}

Text GetMapPlayerModelId() {
  return G_LibPlayerModels_CurrentMapPlayerModelId;
}

/**
 * Pick a random skin for the player and remember it until they leave.
 */
Text GetPlayerRandomizedSkinId(CUser _User, Text _ModelId) {
  declare Text[Text] LibPlayerModels_RandomizedSkins for _User;

  // Preference not set, and there's already a random skin assigned.
  if (LibPlayerModels_RandomizedSkins.existskey(_ModelId)) {
    return LibPlayerModels_RandomizedSkins[_ModelId];
  }

  // Pick a random skin and keep it on the server side.
  declare SkinId = PMData::GetRandomSkinIdByModelId(_ModelId, False);

  if (SkinId != "") {
    LibPlayerModels_RandomizedSkins[_ModelId] = SkinId;
  }

  return SkinId;
}

/**
 * Get the player skin preference or a randomixed skin.
 */
Text GetPlayerSkinId(CUser _User, Text _ModelId) {
  declare BaseModelId = PMData::GetBasePlayerModelId(_ModelId);
  declare PlayerPreferenceId = CSPlayers::GetPlayerSkinId(_User.Login, BaseModelId);

  // Return the preference if it is set.
  if (PlayerPreferenceId != "") {
    return PlayerPreferenceId;
  }

  return GetPlayerRandomizedSkinId(_User, BaseModelId);
}

/**
 * @returns List of all skin IDs the local players has got assigned.
 */
Text[] GetLocalPlayerSkinIds() {
  declare Text[] Result;

  foreach (User in Users) {
    foreach (BaseModelId => __ in PMData::GetAllPlayerModels()) {
      declare PlayerPreferenceId = GetPlayerSkinId(User, BaseModelId);
      if (PlayerPreferenceId == "") continue;
      Result.add(PlayerPreferenceId);
    }
  }

  return Result;
}

/**
 * Check if a model requires special unlock.
 */
Boolean GetPlayerModelRequiresUnlock(Text _ModelId) {
  declare Archetype = PMData::GetPlayerModelArchetype(_ModelId);

  return !C_LibPlayerModels_VehicleIgnoresUnlock.exists(Archetype);
}

/**
 * Check if the player requires additional special unlock to assign the model.
 */
Boolean GetPlayerRequiresUnlock(Text _Login) {
  //return !OPPlayers::HasOpenplanet(_Login);
  return True;
}

/**
 * Check if the player model should override an archetype.
 */
Boolean GetPlayerPreferCustom(Text _Login, Text _ModelId) {
  if (CSPlayers::GetPlayerUseCustomOverArchetype(_Login, _ModelId)) {
    return True;
  }

  return Private_CurrentMapRequiresUnlock() && GetPlayerRequiresUnlock(_Login) && GetPlayerModelRequiresUnlock(_ModelId);
}

/**
 * Assign a model to a player.
 */
Void AssignPlayerModel(CPlayer _Player, Text _ModelId, Text _SkinId, Boolean _PreferCustom) {
  Private_AssignPlayerModel(_Player, _ModelId, _SkinId, _PreferCustom);
}

/**
 * Assign a model to a player (automatic skin).
 */
Void AssignPlayerModel(CPlayer _Player, Text _ModelId, Boolean _PreferCustom) {
  declare SkinId = GetPlayerSkinId(_Player.User, _ModelId);

  AssignPlayerModel(_Player, _ModelId, SkinId, _PreferCustom);
}

/**
 * Assign a model to a player (automatic prefer custom).
 */
Void AssignPlayerModel(CPlayer _Player, Text _ModelId, Text _SkinId) {
  declare PreferCustom = GetPlayerPreferCustom(_Player.User.Login, _ModelId);

  AssignPlayerModel(_Player, _ModelId, _SkinId, PreferCustom);
}

/**
 * Assign a model to a player (automatic skin, prefer custom).
 */
Void AssignPlayerModel(CPlayer _Player, Text _ModelId) {
  declare PreferCustom = GetPlayerPreferCustom(_Player.User.Login, _ModelId);

  AssignPlayerModel(_Player, _ModelId, PreferCustom);
}

/**
 * Assign a model to a player (automatic model from current map, skin, prefer custom).
 */
Void AssignPlayerModel(CPlayer _Player) {
  AssignPlayerModel(_Player, G_LibPlayerModels_CurrentMapPlayerModelId);
}
